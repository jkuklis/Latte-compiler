-- programs ------------------------------------------------

entrypoints Program ;

Program.   Program ::= [TopDef] ;

separator nonempty TopDef "" ;

FnDef.	   TopDef ::= Type Ident "(" [Arg] ")" Block ;

Arg. 	   Arg ::= Type Ident;

separator  Arg "," ;

-- classes -------------------------------------------------

ClDef.     TopDef ::= "class" Ident ClassBlock ;

ClInher.   TopDef ::= "class" Ident "extends" Ident ClassBlock;

ClBlock.   ClassBlock ::= "{" [ClMember] "}" ;

separator  ClMember "" ;

ClAttr.    ClMember ::= Type Ident ";" ;

ClFun.     ClMember ::= Type Ident "(" [Arg] ")" Block ;

-- statements ----------------------------------------------

Block.     Block ::= "{" [Stmt] "}" ;

separator  Stmt "" ;

Empty.     Stmt ::= ";" ;

BStmt.     Stmt ::= Block ;

Decl.      Stmt ::= Type [Item] ";" ;

NoInit.    Item ::= Ident ;

Init.      Item ::= Ident "=" Expr ;

separator nonempty Item "," ;

Ass.       Stmt ::= Ident "=" Expr  ";" ;

ElemAss.   Stmt ::= Ident "[" Expr "]" "=" Expr ";" ;

AttrAss.   Stmt ::= Ident "." Ident "=" Expr ";" ;

SelfAtAss. Stmt ::= "self" "." Ident "=" Expr ";" ;

Incr.      Stmt ::= Ident "++" ";" ;

SelfIncr.  Stmt ::= "self" "." Ident "++" ";" ;

Decr.      Stmt ::= Ident "--" ";" ;

SelfDecr.  Stmt ::= "self" "." Ident "--" ";" ;

Ret.       Stmt ::= "return" Expr ";" ;

VRet.      Stmt ::= "return" ";" ;

Cond.      Stmt ::= "if" "(" Expr ")" Stmt  ;

CondElse.  Stmt ::= "if" "(" Expr ")" Stmt "else" Stmt  ;

While.     Stmt ::= "while" "(" Expr ")" Stmt ;

ForEach.   Stmt ::= "for" "(" Type Ident ":" Ident ")" Stmt ;

SExp.      Stmt ::= Expr  ";" ;

-- Types ---------------------------------------------------

Int.       Type1 ::= "int" ;

Str.       Type1 ::= "string" ;

Bool.      Type1 ::= "boolean" ;

Void.      Type1 ::= "void" ;

Class.     Type1 ::= Ident ;

Array.     Type ::= Type1 "[]" ;

coercions  Type 1 ;

-- internal   InhClass. Type ::= Ident Ident ;

-- internal   Fun. Type ::= Type "(" [Type] ")" ;

-- separator Type "," ;

-- Expressions ---------------------------------------------

EVar.      Expr6 ::= Ident ;

ELitInt.   Expr6 ::= Integer ;

ELitTrue.  Expr6 ::= "true" ;

ELitFalse. Expr6 ::= "false" ;

EApp.      Expr6 ::= Ident "(" [Expr] ")" ;

EString.   Expr6 ::= String ;

EElem.     Expr6 ::= Ident "[" Expr "]" ;

EArrayNew. Expr6 ::= "new" Type1 "[" Expr "]" ;

ENull.     Expr6 ::= "(" Ident ")" "null" ;

ENew.      Expr6 ::= "new" Ident ;

EASelf.    Expr6 ::= "self" "." Ident ;

EMSelf.    Expr6 ::= "self" "." Ident "(" [Expr] ")" ;

EAttr.     Expr6 ::= Ident "." Ident ;

EMethod.   Expr6 ::= Ident "." Ident "(" [Expr] ")" ;

Neg.       Expr5 ::= "-" Expr6 ;

Not.       Expr5 ::= "!" Expr6 ;

EMul.      Expr4 ::= Expr4 MulOp Expr5 ;

EAdd.      Expr3 ::= Expr3 AddOp Expr4 ;

ERel.      Expr2 ::= Expr2 RelOp Expr3 ;

EAnd.      Expr1 ::= Expr2 "&&" Expr1 ;

EOr.       Expr ::= Expr1 "||" Expr ;

coercions  Expr 6 ;

separator  Expr "," ;

-- operators -----------------------------------------------

Plus.      AddOp ::= "+" ;

Minus.     AddOp ::= "-" ;

Times.     MulOp ::= "*" ;

Div.       MulOp ::= "/" ;

Mod.       MulOp ::= "%" ;

LTH.       RelOp ::= "<" ;

LE.        RelOp ::= "<=" ;

GTH.       RelOp ::= ">" ;

GE.        RelOp ::= ">=" ;

EQU.       RelOp ::= "==" ;

NE.        RelOp ::= "!=" ;

-- comments ------------------------------------------------

comment    "#" ;

comment    "//" ;

comment    "/*" "*/" ;
